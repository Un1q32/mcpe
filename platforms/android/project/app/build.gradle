plugins {
    id 'com.android.application'
}

def useCcache = System.getenv("USE_CCACHE") == "1"

android {
    namespace = 'org.nbcraft'
    compileSdk 33

    // Employ a hack which copies the assets folder in to the local assets folder.
    tasks.register('CopyAssets', Copy) {
        from '../../../../game/assets'
        into 'src/main/assets'
    }
    sourceSets.main {
        // Another hack to automatically get our icons
        assets.srcDir '../../../../game/assets/app'
        def iconDir = file('../../../../game/assets/app/icons/mipmap')
        def resDir = file('src/main/res')
        def densityMap = [
                'icon_48x48.png'  : 'mipmap-mdpi',
                'icon_72x72.png'  : 'mipmap-hdpi',
                'icon_96x96.png'  : 'mipmap-xhdpi',
                'icon_144x144.png': 'mipmap-xxhdpi',
                'icon_192x192.png': 'mipmap-xxxhdpi'
        ]

        densityMap.each { fileName, folderName ->
            def sourceFile = new File(iconDir, fileName)
            def targetDir = new File(resDir, folderName)
            if (sourceFile.exists()) {
                targetDir.mkdirs()
                // Copies and renames it to just "icon.png" so AndroidManifest.xml is happy
                new File(targetDir, 'icon.png').bytes = sourceFile.bytes
            } else {
                println "Warning: Could not find ${fileName} in ${iconDir}"
            }
        }
    }

    applicationVariants.all { variant ->
        // Ensure the custom task is executed before building the APK
        variant.preBuildProvider.configure { dependsOn(CopyAssets) }
    }

    defaultConfig {
        applicationId "org.nbcraft"
        minSdk 16
        // Don't inspect the target SDK. You'd better not intend to upload this to the Google Play Store.
        //noinspection ExpiredTargetSdkVersion
        targetSdk 33
        versionCode 1
        versionName "1.0"

        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16', '-DANDROID_STL=c++_static', '-DNBC_PLATFORM=android', '-DNBC_AUDIO_LIBRARY=opensl', '-DNBC_GFX_API=OGL'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                if (useCcache) {
                    arguments += '-DCMAKE_C_COMPILER_LAUNCHER=ccache'
                    arguments += '-DCMAKE_CXX_COMPILER_LAUNCHER=ccache'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path '../../../../CMakeLists.txt'
            version = '3.22.1'
        }
    }
}

dependencies {
}
